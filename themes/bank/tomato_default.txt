@name Bank Default Theme
if (first()) {
    #include "library/tomato_lib"
    Profile = noentity()
    BankName = "BetterBank"
    Screen = 1
    
    #Colours
    Dark = vec(27, 38, 44)
    Secondary = vec(15, 76, 117)
    Primary = vec(50, 130, 184)
    Light = vec(187, 225, 250)
    
    #-------- Global Screens ---------#
    function background() {
        #Global Screen Vars
        EGP:egpClear()
        createBox("Background",size(vec2(ScreenXY,ScreenXY)), color(Dark), coords(vec2(0,0)))
        createText("A Tomato Production",size(16),align(1),coords(vec2(ScreenXY/2,ScreenXY-20))) 
        createBox("Bar1",size(vec2(20,ScreenXY)), color(Primary), coords(vec2(0,0)))
        createBox("Bar2",size(vec2(20,ScreenXY)), color(Primary), coords(vec2(ScreenXY-20,0)))
    }
    
    function logo(Size,YOffSet) {
        #Bank Logo
        createIsosceles("Logo1", size(5*Size),color(Primary), coords(vec2(ScreenXY/2, 40+YOffSet)))
        createIsosceles("Logo2", size(4.5*Size),color(Dark), coords(vec2(ScreenXY/2, 45+YOffSet)))
        createIsosceles("Logo3", size(4*Size),color(Primary), coords(vec2(ScreenXY/2, 50+YOffSet)))
        createBox("Logo4", size(vec2(20*Size,0.8*Size)), color(Primary), coords(vec2(ScreenXY/2-10*Size,9.5*Size+YOffSet))) 
        createText("Logo5", size(7*Size),color(Primary),align(1),coords(vec2(ScreenXY/2,10.5*Size+YOffSet)))
        createBox("Logo6", size(vec2(20*Size,0.8*Size)), color(Primary), coords(vec2(ScreenXY/2-10*Size,18*Size+YOffSet)))
        createBox("Logo7", size(vec2(22*Size,0.8*Size)), color(Primary), coords(vec2(ScreenXY/2-11*Size,19.5*Size+YOffSet)))
        createBox("Logo8", size(vec2(24*Size,0.8*Size)), color(Primary), coords(vec2(ScreenXY/2-12*Size,21*Size+YOffSet)))
        EGP:egpSetText(Elements:get("Logo5"),BankName)   
    }
    
    #-------- Customer Screens -------#
    function drawInitial(Alpha) {
        background()
        logo(10,-10)
        
        #Welcome text
        createText("Welcome",size(20),align(1),coords(vec2(ScreenXY/2,ScreenXY/2+60)), Alpha)
        createText("Press [E] to start",size(18),align(1),coords(vec2(ScreenXY/2,ScreenXY/2+90)), Alpha)
        EGP:egpSetText(Elements:get("Welcome"),"Welcome to " + BankName)
    }
    
    function drawTutorial() {
        background()
        createText("Text1",size(30),align(1),coords(vec2(ScreenXY/2,10)))
        createText("Text2",size(30),align(1),coords(vec2(ScreenXY/2,160)))
        createText("Text3",size(30),align(1),coords(vec2(ScreenXY/2,310)))
        EGP:egpSetText(Elements:get("Text1"),"Tier 1")
        EGP:egpSetText(Elements:get("Text2"),"Tier 2")
        EGP:egpSetText(Elements:get("Text3"),"Tier 3")
        createTextLayout("Tier1", size(vec2(ScreenXY/2,150)), color(Light), align(1), coords(vec2(ScreenXY/4,40)))
        createTextLayout("Tier2", size(vec2(ScreenXY/2,150)), color(Light), align(1), coords(vec2(ScreenXY/4,190)))
        createTextLayout("Tier3", size(vec2(ScreenXY/2,150)), color(Light), align(1), coords(vec2(ScreenXY/4,340)))
        
        EGP:egpSetText(Elements:get("Tier1"),
        "Basic storage in a communal vault. Seperate boxes locked to each costumer.")
        EGP:egpSetText(Elements:get("Tier2"),
        "Private vault storage. Medium sized vault. Can fit 1 printer rack and some shipments.")
        EGP:egpSetText(Elements:get("Tier3"),
        "Private vault storage. Large sized vault. Can fit multiple racks and shipments.")
        
        
        createBox("Back1",size(vec2(240,50)), color(Primary), coords(vec2(ScreenXY/2-120,ScreenXY/2+182)))
        createText("< Back",size(20),align(1),coords(vec2(ScreenXY/2,ScreenXY-60)))
    }
    
    function drawMenu() {
        background()
        logo(10,-10)
        EGP:egpRemove(Elements:get("Welcome"))
        EGP:egpRemove(Elements:get("Press [E] to start"))
        
        
        createBox("Button1",size(vec2(240,50)), color(Primary), coords(vec2(ScreenXY/2-120,ScreenXY/2-14)))
        createBox("Button2",size(vec2(240,50)), color(Primary), coords(vec2(ScreenXY/2-120,ScreenXY/2+36)))
        createBox("Button3",size(vec2(240,50)), color(Primary), coords(vec2(ScreenXY/2-120,ScreenXY/2+86)))
        createBox("Button4",size(vec2(240,50)), color(Primary), coords(vec2(ScreenXY/2-120,ScreenXY/2+136)))
        
        createBox("Row1",size(vec2(240,2)), color(Light), coords(vec2(ScreenXY/2-120,ScreenXY/2+35)))
        createBox("Row2",size(vec2(240,2)), color(Light), coords(vec2(ScreenXY/2-120,ScreenXY/2+85)))
        createBox("Row3",size(vec2(240,2)), color(Light), coords(vec2(ScreenXY/2-120,ScreenXY/2+135)))
        
        createText("Profile",size(16),align(1),coords(vec2(ScreenXY/2,ScreenXY/2-30)))
        createText("Text1",size(20),align(1),coords(vec2(ScreenXY/2+15,ScreenXY/2)))
        createText("Text2",size(20),align(1),coords(vec2(ScreenXY/2+15,ScreenXY/2+50)))
        createText("Text3",size(20),align(1),coords(vec2(ScreenXY/2+15,ScreenXY/2+100)))
        createText("Text4",size(20),align(1),coords(vec2(ScreenXY/2+15,ScreenXY/2+150)))
        
        createMaterial("Icon1", size(vec2(20,20)), coords(vec2(ScreenXY/2-45,ScreenXY/2)), 
        "materials/porky/user.png")
        createMaterial("Icon2", size(vec2(20,20)), coords(vec2(ScreenXY/2-45,ScreenXY/2+50)), 
        "materials/tauf4/jobs/banker.png")
        createMaterial("Icon3", size(vec2(20,20)), coords(vec2(ScreenXY/2-45,ScreenXY/2+100)), 
        "materials/tauf4/help.png")
        createMaterial("Icon4", size(vec2(20,20)), coords(vec2(ScreenXY/2-25,ScreenXY/2+170)), 
        "materials/underground/consoles/arrow.png")
        EGP:egpAngle(Elements:get("Icon4"), 180)
        EGP:egpSetText(Elements:get("Profile"),"Profile: " + formatName(Profile:name(), 20))
        EGP:egpSetText(Elements:get("Text1"),"Account")
        EGP:egpSetText(Elements:get("Text2"),"Storage")
        EGP:egpSetText(Elements:get("Text3"),"Tutorial")
        EGP:egpSetText(Elements:get("Text4"),"Logout")
    }
    
    function drawAccount() {
        background()
        logo(10,-10)
        
        createBox("Button1",size(vec2(240,50)), color(Primary), coords(vec2(ScreenXY/2-120,ScreenXY/2-14)))
        createBox("Button2",size(vec2(240,50)), color(Primary), coords(vec2(ScreenXY/2-120,ScreenXY/2+36)))
        createBox("Button3",size(vec2(240,50)), color(Primary), coords(vec2(ScreenXY/2-120,ScreenXY/2+86)))
        
        createBox("Row1",size(vec2(240,2)), color(Light), coords(vec2(ScreenXY/2-120,ScreenXY/2+35)))
        createBox("Row2",size(vec2(240,2)), color(Light), coords(vec2(ScreenXY/2-120,ScreenXY/2+85)))
        
        createText("Profile",size(16),align(1),coords(vec2(ScreenXY/2,ScreenXY/2-30)))
        createText("Text1",size(20),align(1),coords(vec2(ScreenXY/2+15,ScreenXY/2)))
        createText("Text2",size(20),align(1),coords(vec2(ScreenXY/2+15,ScreenXY/2+50)))
        createText("Text3",size(20),align(1),coords(vec2(ScreenXY/2+15,ScreenXY/2+100)))

        createBox("Back1",size(vec2(240,50)), color(Primary), coords(vec2(ScreenXY/2-120,ScreenXY/2+182)))
        createText("< Back",size(20),align(1),coords(vec2(ScreenXY/2,ScreenXY-60)))
        
        createMaterial("Icon1", size(vec2(20,20)), coords(vec2(ScreenXY/2-65,ScreenXY/2)), 
        "materials/porky/shield.png")
        createMaterial("Icon2", size(vec2(20,20)), coords(vec2(ScreenXY/2-75,ScreenXY/2+50)), 
        "zerochain/zerolib/gameicons/lockedchest.png")
        createMaterial("Icon3", size(vec2(20,20)), coords(vec2(ScreenXY/2-75,ScreenXY/2+100)), 
        "materials/porky/wallet.png")
        
        
        EGP:egpSetText(Elements:get("Profile"),"Profile: " + formatName(Profile:name(), 20))
        EGP:egpSetText(Elements:get("Text1"),"Membership")
        EGP:egpSetText(Elements:get("Text2"),"Check Storage")
        EGP:egpSetText(Elements:get("Text3"),"Request Loan")
    }
    
    function drawContract() {
        background()
        local Name = formatName(Profile:name(), 20)
        local Date = dateUTC()
        local DateFormat = toString(Date["day", number])+"/"+toString(Date["month", number])+"/"+toString(Date["year", number])
        createText("Depository Contract",size(30),align(1),coords(vec2(ScreenXY/2,10)))
        createTextLayout("Contract", size(vec2(ScreenXY-80,ScreenXY)), color(Light), align(0), coords(vec2(40,50)))
        
        EGP:egpSetText(Elements:get("Contract"),
        "This agreement between "+Name+" and "+BankName+" is hereby entered into on this date: "+DateFormat+".
        \nPurpose: "+BankName+" and "+Name+" will be entering into storage operations involving "+BankName+"'s establishment, which require "+Name+" to abide by the following regulations on an ongoing basis. 
        \n"+Name+" hereby agrees to the following:
        \n1. Confidential information related to other client's storage and/or account may not be shared.
        \n2. "+BankName+" has the right to claim ownership of items left in unpaid accounts after 15 minutes. 
        \n3. To abide by the listed KOS rules. 
        \n4. To be notified of illicit activities and may choose to assist in safeguarding their personal belongings. 
        ")
        createBox("Back1",size(vec2(80,40)), color(Primary), coords(vec2(30,ScreenXY-50)))
        createText("< Back",size(20),align(1),coords(vec2(70,ScreenXY-40)))
        createBox("Okay1",size(vec2(80,40)), color(Primary), coords(vec2(ScreenXY-110,ScreenXY-50)))
        createText("Okay >",size(20),align(1),coords(vec2(ScreenXY-65,ScreenXY-40)))
    }
    
    function drawLoan() {
        background()
        local Name = formatName(Profile:name(), 20)
        createText(BankName+" Loaning System",size(30),align(1),coords(vec2(ScreenXY/2,10)))
        createText("I, "+Name+" acknowledge my obligations to: ", size(20),align(1),coords(vec2(ScreenXY/2,50)))
        
        createCheckBox("Use funds for their intended purpose.", size(vec2(40,20)),  coords(vec2(ScreenXY/8,100)))
        createCheckBox("Repay loan instalments on a regular basis.", size(vec2(40,20)),  coords(vec2(ScreenXY/8,140)))
        createCheckBox("Use the loan within the "+BankName+"'s establishment", size(vec2(40,20)),  coords(vec2(ScreenXY/8,180)))
        
        createDropdown("Select Job:", size(vec2(ScreenXY / 4+50, 25)), coords(vec2(ScreenXY/8,220)))
        createDropdown("Select Reason:", size(vec2(ScreenXY / 4+60, 25)), coords(vec2(ScreenXY/2-50 + ScreenXY/8,220)))
        
        createText("Loan Amount: $0", size(18), align(1), coords(vec2(ScreenXY/2,370)))
        createSlider("Loan", size(vec2(ScreenXY / 2, 20)), coords(vec2(ScreenXY / 4,390)))
        
        createBox("Back1", size(vec2(80,40)), color(Primary), coords(vec2(30,ScreenXY-50)))
        createText("< Back", size(20),align(1),coords(vec2(70,ScreenXY-40)))
        createBox("RequestButton", size(vec2(110,40)), color(Primary), coords(vec2(ScreenXY-140,ScreenXY-50)))
        createText("Request >", size(20),align(1),coords(vec2(ScreenXY-80,ScreenXY-40)))
    }
    
    function drawBooking() {
        background()
        
        #Booking Table
        createBox("TableBackground", size(vec2(ScreenXY-60,50)), color(White), coords(vec2(30,50)))
        createBox("Header", size(vec2(ScreenXY-60,41)), color(Secondary), coords(vec2(30,10)))
        createText("Storage Type", size(16),coords(vec2(40,20)))
        createText("Size", size(16),coords(vec2(220,20)))
        createText("Price/Hour*", size(16),coords(vec2(285,20)))
        createText("Availability", size(16),coords(vec2(402.5,20)))
        
        #Columns
        createBox("Column1", size(vec2(3,90)), color(Dark), coords(vec2(210-1,52)))
        createBox("Column2", size(vec2(3,90)), color(Dark), coords(vec2(255-1,52)))
        createBox("Column3", size(vec2(3,90)), color(Dark), coords(vec2(390-1,52)))
        
        createBox("Back1", size(vec2(80,40)), color(Primary), coords(vec2(30,ScreenXY-50)))
        createText("< Back", size(20),align(1),coords(vec2(70,ScreenXY-40)))
    }
    
    function drawMembership(Customer:table, Taxes:string, Price) {
        background()
        if (Customer:count()) {
            local Name = formatName(Customer["Name", string], 20)
            local Membership = Customer["Membership", string]
            local Centre = ScreenXY/2
            local XLeft = ScreenXY/2-ScreenXY/4
            local XRight = ScreenXY/2+ScreenXY/4
            createText("Memberships", size(40), color(Light), align(1), coords(vec2(Centre,20)))
            createTextLayout("Welcome", size(vec2(ScreenXY-ScreenXY/4,250)), color(Light), align(1), coords(vec2(ScreenXY/8,80)))
            EGP:egpSetText(Elements:get("Welcome"),
            "Hi " + Name + ", welcome to " + BankName + "'s membership system. Each level of membership corresponds to a tax level.
            \nThe current tax settings are:\n\n " + Taxes)
            
            createText("CurrentMembership", size(20), color(Light), align(1), coords(vec2(Centre,325)))
            EGP:egpSetText(Elements:get("CurrentMembership"), Name + ": " + Membership + " member")
            
            createText("Price", size(20), color(Light), align(1), coords(vec2(Centre,405)))
            EGP:egpSetText(Elements:get("Price"), "Current Price: $" + formatNumber(Price))
            
            
            createBox("Purchase",size(vec2(240,50)), color(Primary), coords(vec2(ScreenXY/2-120,350)))
            createText("Upgrade Membership",size(20),align(1),coords(vec2(ScreenXY/2,365)))
            
            if (Membership == "staff" || Membership == "diamond") {
                EGP:egpSetText(Elements:get("Upgrade Membership"), "Max Tier Reached")
            }
            
            createBox("Back1", size(vec2(80,40)), color(Primary), coords(vec2(30,ScreenXY-50)))
            createText("< Back", size(20),align(1),coords(vec2(70,ScreenXY-40)))
        }
        else {
            logo(10,-10)
            createTextLayout("Error", size(vec2(ScreenXY/2,ScreenXY/2)), color(Light), align(1), coords(vec2(ScreenXY/2-ScreenXY/4,ScreenXY/2)))
            EGP:egpSetText(Elements:get("Error"), "Please purchase a vault to start a " + BankName + " account.")
        }
    }
    
    function drawStorage(Customer:table, CurrentDate:string, Tax) {
        background()
        if (Customer:count()) {
            local Name = Customer["Name", string]
            local SessionProfit = Customer["Profit", table][CurrentDate, number]
            local Tenure = Customer["Tenure", string]
            local TotalProfit = Customer["Profit", table]["Total", number]
            local Membership = Customer["Membership", string]
            local Printers = Customer["Printers", table][CurrentDate, table]
            
            local Centre = ScreenXY/2
            local XLeft = ScreenXY/2-ScreenXY/4
            local XRight = ScreenXY/2+ScreenXY/4
            createText("Name", size(40), color(Light), align(1), coords(vec2(Centre,20)))
            createText("Membership", size(20), color(Light), align(1), coords(vec2(Centre,55)))
            
            createText("Tenure", size(20), color(Light), align(0), coords(vec2(XLeft - 80,100)))
            createText("Total Profit", size(20), color(Light), align(0), coords(vec2(XLeft - 80,130)))
            createText("Profit", size(20), color(Light), align(0), coords(vec2(XLeft - 80,160)))
             
            createText("Perks", size(25), color(Light), align(1), coords(vec2(XRight+15,80)))
            createText("AutoPay", size(20), color(Light), align(0), coords(vec2(XRight,110)))
            createText("Taxcut", size(20), color(Light), align(0), coords(vec2(XRight,130)))
            createText("Insurance", size(20), color(Light), align(0), coords(vec2(XRight,150)))
            
            local Perk2 = 0
            local Perk3 = 0
            if (Tax < 25) {Perk2 = 1}
            if (Tax < 15) {Perk3 = 1}
            createTick("Tick1", vec2(15,15), coords(vec2(XRight-30,115)), 1)
            createTick("Tick2", vec2(15,15), coords(vec2(XRight-30,135)), Perk2)
            createTick("Tick3", vec2(15,15), coords(vec2(XRight-30,155)), Perk3)
            
            createText("Storage", size(30), color(Light), align(1), coords(vec2(Centre,200)))
            
            createText("Printers", size(20), color(Light), align(1), coords(vec2(XLeft,230)))
            createText("Items", size(20), color(Light), align(1), coords(vec2(XRight,230)))
            
            
            createBox("CollectBG", size(vec2(220,180)), color(Light), coords(vec2(XLeft-100,265)))
            createBox("ItemsBG", size(vec2(220,180)), color(Light), coords(vec2(XRight-120,265)))
            
            local Table = table()
            for (I=1, Printers:count()) {
                local Printer = Printers[I, string]
                local Name = Printer[9]:upper() + Printer:sub(10) + " Printer "
                if (Table:exists(Printer)) {
                    Table[Name, number] = Table[Printer, number] + 1
                }
                else {
                    Table[Name, number] = 1
                }
            }
            createTextLayout("PrinterTable", size(vec2(200,160)), color(Dark), align(1), coords(vec2(XLeft-90,275)))
            EGP:egpSetText(Elements:get("PrinterTable"), Table:toString())
            
            
            EGP:egpSetText(Elements:get("Name"), formatName(Name, 16))
            EGP:egpSetText(Elements:get("Membership"), Membership + " member")
            EGP:egpSetText(Elements:get("Tenure"), "Date joined: " + Tenure)
            EGP:egpSetText(Elements:get("Total Profit"), "Total Profit: $" + formatNumber(TotalProfit))
            EGP:egpSetText(Elements:get("Profit"), "Session Profit: $" + formatNumber(SessionProfit))
        }
        else {
            logo(10,-10)
            createTextLayout("Error", size(vec2(ScreenXY/2,ScreenXY/2)), color(Light), align(1), coords(vec2(ScreenXY/2-ScreenXY/4,ScreenXY/2)))
            EGP:egpSetText(Elements:get("Error"), "Please purchase a vault to start a " + BankName + " account.")
        }
        createBox("Back1", size(vec2(80,40)), color(Primary), coords(vec2(30,ScreenXY-50)))
        createText("< Back", size(20),align(1),coords(vec2(70,ScreenXY-40)))
    }
    
    function drawLoading() {
        background()
        logo(10,-10)
        for (I=1,12) {
            createRoundedBox("Loading"+I, size(vec2(5,15)), color(Light), coords(vec2(ScreenXY/2,ScreenXY/2)))
            local ID = Elements:get("Loading"+I)
            EGP:egpAngle(ID,vec2(ScreenXY/2,ScreenXY/2+50),vec2(-2.5,15),(I-1)*30)
            EGP:egpAlpha(ID,255-(I-1)*21.25)
        }
    }
    
    function drawToDo() {
        background()
        logo(10,-10)
        createText("To DO", size(16),align(1),coords(vec2(ScreenXY/2,ScreenXY/2-30)))
        
        createBox("Back1", size(vec2(80,40)), color(Primary), coords(vec2(ScreenXY/2-40,ScreenXY-100)))
        createText("< Back", size(20),align(1),coords(vec2(ScreenXY/2,ScreenXY-90)))
    }
    
    #-------- Admin Screens ---------#
    function drawAdminInitial(Alpha) {
        background()
        logo(10,-10)
        
        #Welcome text
        createText("Welcome",size(20),align(1),coords(vec2(ScreenXY/2,ScreenXY/2+60)), Alpha)
        createText("Press [E] to start",size(18),align(1),coords(vec2(ScreenXY/2,ScreenXY/2+90)), Alpha)
        EGP:egpSetText(Elements:get("Welcome"), BankName + "'s Admin Panel")
    }
    
    function drawAdminPanel(Alpha, Keys) {
        local Page = Screen-2
        background()
        #Total Profit Made
        createText("Admin Panel",size(30),align(1),coords(vec2(ScreenXY/2,20)))
        
        if (Page > 1) {
            createBox("BackButton", size(vec2(80,40)), color(Primary), coords(vec2(30,ScreenXY-50)))
            createText("< Prev", size(20),align(1),coords(vec2(70,ScreenXY-40)))
        }
        if (Page * 6 < Keys) {
            createBox("NextButton", size(vec2(80,40)), color(Primary), coords(vec2(ScreenXY-110,ScreenXY-50)))
            createText("Next >", size(20),align(1),coords(vec2(ScreenXY-67,ScreenXY-40)))
        }
        
        createText("Page",size(16),align(1),coords(vec2(ScreenXY/2,ScreenXY-40)))
        EGP:egpSetText(Elements:get("Page"), "Page: "+toString(Page))
    }
    
    function drawUserInfo(I, Name:string, Membership:string, Profit, Items) {
        local X = 36
        local Y = 70+I*53
        local TxtOffset = 13
        createBox("UserBG"+I, size(vec2(440,50)), color(Light), coords(vec2(X,Y)))
        createBox("NameBG"+I, size(vec2(440,50)), color(Light), coords(vec2(X,Y)))
        createText("Name"+I, size(20), color(Dark), align(0), coords(vec2(X + 20, Y + TxtOffset)))
        createText("Profit"+I, size(20), color(Dark), align(0), coords(vec2(X + 180, Y + TxtOffset)))
        createText("Storage"+I, size(20), color(Dark), align(0), coords(vec2(X + 340, Y + TxtOffset)))
        EGP:egpSetText(Elements:get("Name"+I), formatName(Name, 16))
        EGP:egpSetText(Elements:get("Profit"+I), "$"+formatNumber(Profit)+" ("+Membership+")")
        EGP:egpSetText(Elements:get("Storage"+I), "Printers: "+Items)
    }
}
