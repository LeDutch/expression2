@name Tomato VIP Access
@inputs [Door1Pos Door2Pos Door3Pos Door4Pos]:entity
@outputs [Door1 Door2 Door3 Door4]:number
@persist MembershipPrice VIPNum
@persist [VIPs]:array [Charge Buyer]:entity
@trigger 

###################################################
#Coded by Bob Tomato, any changes or reqs /pm me :)
###################################################

if (first() || dupefinished() || ~EGP) {
    #include "library/tomato_lib"
    
    Dark = vec(43, 43, 40)
    Primary = vec(227, 176, 75)
    Secondary = vec(241, 214, 171)
    Light = vec(248, 248, 248)
    
    Owner = owner()
    VIPNum = 0
    MembershipPrice = 20000
    Charge = findPlayerBySteamID("STEAM_0:0:67228851")
    VIPs = array()
	Prefix = "BLACK PYRAMID"
    
    function updateVIPs() {
        local VIPs = VIPs:count()
        local XOffset = 0
        local YOffset = VIPNum*50
        if(VIPs < VIPNum) {
            EGP:egpRemove(Elements:get("Person "+VIPNum))
            EGP:egpRemove(Elements:get("Row"+VIPNum))
            VIPNum--
            EGP:egpSize(Elements:get("TableBackground"),size(vec2(ScreenXY-20,YOffset)))
        }
        else {
            if (VIPNum > 6) {
                XOffset = ScreenXY/2
                YOffset = (VIPNum - 6) * 50
            }
            else {
                createBox("Row"+VIPNum,size(vec2(ScreenXY-20,1)), color(Secondary), coords(vec2(10,140+YOffset)))
                EGP:egpSize(Elements:get("TableBackground"),size(vec2(ScreenXY-20,YOffset)))
            }
            createText("Person "+VIPNum, size(22), color(Dark), coords(vec2(20 + XOffset,105+YOffset)))
        }
    }
    
    function number doorCheck(Player:entity, Door:entity) {
        local Prox = 50
        local ZOffset = 70
        return inrange(Player:pos(), Door:pos()-vec(Prox,Prox,Prox+ZOffset), Door:pos()+vec(Prox,Prox,0))
    }
    
    function startLease(Buyer:entity) {
        local ID = VIPs:find(Buyer)
        if (ID) {
            Timer:addTime(toString(ID), 3600)
            Buyer:msg("Thank you "+Buyer:name()+" for renewing your VIP membership!")
        }
        else {
            VIPs:pushEntity(Buyer)
            VIPNum++
            Timer:addTime(toString(VIPNum), 3600)
            Buyer:msg("Thank you " + Buyer:name() + " for purchasing VIP membership, a staff member will be with you shortly.")
        }
        if (Charge:isValid() & Charge != owner()) {
            moneyGive(Charge, MembershipPrice * 0.05)
        }
        updateVIPs()
    }
    
    function endLease(VIP:entity) {
        local ID = VIPs:find(VIP)
        VIPs:removeEntity(ID)
        Timer:removeString(toString(ID))
        updateVIPs()
    }
    
    EGP:egpClear()
    
    #Generic
    createBox("Background",size(vec2(ScreenXY,ScreenXY)), color(Dark), coords(vec2(0,0)))
    createText("Title",size(36),align(1),coords(vec2(ScreenXY/2,0)))
    createText("A Tomato Production",size(16),align(1),coords(vec2(ScreenXY/2,30))) 
    createText("Description",size(24),align(1),coords(vec2(ScreenXY/2,60))) 

    #Booking Table
    createBox("TableBackground",size(vec2(ScreenXY-20,50)), color(White), coords(vec2(10,140)))
    createBox("Header",size(vec2(ScreenXY-20,40)), color(Primary), coords(vec2(10,100)))
    createText("VIP Name",size(16),coords(vec2(20,110)))
    createTriangle("Pyramid1", size(10),color(Secondary), coords(vec2(ScreenXY-110, 120)))
    createTriangle("Pyramid2", size(15),color(Secondary), coords(vec2(ScreenXY-80, 115)))
    createTriangle("Pyramid3", size(20),color(Secondary), coords(vec2(ScreenXY-40, 110)))
    
    #Footer info
    createBox("InfoBox",size(vec2(190,30)), color(Dark), coords(vec2(10,455)))
    createIcon("InfoIcon", size(20), coords(vec2(10,460)))
    createText("Press to see VIP perks.",size(16),coords(vec2(40,460)))
    
    createBox("ContactBox",size(vec2(190,30)), color(Dark), coords(vec2(ScreenXY/2+100,455)))
    createMaterial("ContactIcon",size(vec2(20,20)), coords(vec2(ScreenXY/2+100, 458)), "gui/info")
    createText("Contact Security",size(16),coords(vec2(ScreenXY/2+130,460)))
    
    #Custom Text
    EGP:egpSetText(Elements:get("Title"),"VIP SYSTEMS")
    EGP:egpSetText(Elements:get("Description"),"Gain Access to BLACK PYRAMID's VIP section")
}

interval(200)

#User Input
if (~User) {
    #User hovers over VIP perks
    if(cursorPos(Elements:get("InfoBox")) & User:keyPressed("E")) {
        User:msg("Hello, thank you for inquiring about our exclusive VIP service!")
        User:msg("VIPs get a range of benefits at the club, including their own lounge, bar, TV and gambling services.")
        User:msg("Additionally, staff members offer services such as item storage, complementary drinks or weapons.")
        User:msg("No questions are asked about the nature of the items stored.")
    }
    
    #User presses contact button 
    if(cursorPos(Elements:get("ContactBox")) & User:keyPressed("E")){
        soundPlay(19,0,"hl1/fvox/bell.wav")
        Owner:msg("" + User:name() + " needs some security assistance.")
    }
    
    #User presses anywhere else   
    if(cursorPos(Elements:get("Background")) & !cursorPos(Elements:get("ContactBox")) & !cursorPos(Elements:get("InfoBox"))
        & User:keyPressed("E")) {
        if (VIPNum < 12) {
            Buyer = User
            if (VIPs:find(Buyer)) {
                moneyRequest(Buyer,MembershipPrice, "Renew VIP membership?")
            }
            else {
                moneyRequest(Buyer,MembershipPrice, "Purchase VIP?")
            }
        }
        else {
            User:msg("Maximum amount of VIPs. Please come back later!")
        }
    } 
}

#Person pays
if(moneyClk()){
    if (Owner != owner()) {
        moneyGive(Owner, MembershipPrice - MembershipPrice * 0.05)
        Owner:msg("Paid: $" + Amount + " (-5 percent commission fee)")
    }
    startLease(Buyer)
}

#Timer interval
for (I = 1,VIPNum){
    local ID = toString(I)
    local Time = Timer:getTime(ID)
    local Player = VIPs[I,entity]
    if(Time>0.2){
        Timer:reduceTime(ID, 0.2)
        local Minutes = round(Time/60)
        local Text = ""
        if (Minutes < 2) {Text = "min"} 
        else {Text = " mins"}
        EGP:egpSetText(Elements:get("Person "+I),Player:name()+ ": " + Minutes + Text)
        }
    elseif(Time == 0.2){
        endLease(Player)  
    }
    if(Time == 600){ # 10 min
        Player:msg("Your vip status expires in 10 minutes, head down to board to renew.")
    }
    elseif(Time == 300){ # 5 min
        Player:msg("Your vip status expires in 5 minutes, head down to board to renew.")
    }
    Door1 = doorCheck(Player, Door1Pos)
    Door2 = doorCheck(Player, Door2Pos)
    Door3 = doorCheck(Player, Door3Pos)
    Door4 = doorCheck(Player, Door4Pos)
}

#Chat commands 
runOnChat(1)
if(chatClk(Owner))  
{  
    if(Owner:lastSaid():index(1) == "!")  
    {  
        Arguments = Owner:lastSaid():sub(2):explode(" ")
        Command = Arguments:shiftString() 
        if (Command == "title")  
        {
            Title = ""
            for(I = 1,Arguments:count()){
                Title += Arguments[I,string] 
                Title += " " 
            }                  
            EGP:egpSetText(Elements:get("Title"),Title)
        }
        elseif (Command == "desc")  
        {
            Title = ""
            for(I = 1,Arguments:count()){
                Title += Arguments[I,string] 
                Title += " " 
            }                  
            EGP:egpSetText(Elements:get("Description"),Title)
        }
        elseif (Command == "price")  
        {
            Price = Arguments[1,string]             
            MembershipPrice = Price:toNumber()
            Owner:msg("Membership price set to " + MembershipPrice + ".")
        }
        elseif (Command == "evict") {
            local VIP = findPlayerByName(Arguments[1,string] )
            local VIPNum = VIPs:find(VIP)
            if (VIP:isValid()) {
                if (VIPNum) {
                    Timer:setTime(toString(VIPNum), 0.1)
                    Owner:msg("Evicted VIP: " + VIP:name())
                    endLease(VIP) 
                }   
                else {
                    Owner:msg("Multiple players found under that name.")
                } 
            }    
            else {Owner:msg("Please select a valid VIP.")}
        }
        elseif (Command == "addtime") {
            local VIP = findPlayerByName(Arguments[1,string] )
            local VIPNum = toString(VIPs:find(VIP))
            local Time = Arguments[2,string]:toNumber() 
            if (VIP) {
                Timer:addTime(VIPNum, Time*60)
                Owner:msg("Added " + Time + " minutes to VIP: " + VIPs[VIPNum:toNumber(),entity]:name())
            }
            else {Owner:msg("Please select a valid VIP.")}            
        }
        elseif (Command == "notice") {
            Text = ""
            for(I = 1,Arguments:count()){
                Text += Arguments[I,string] 
                Text += " " 
            }                  
            for (I = 1, VIPs:count()) {
                VIPs[I,entity]:msg(""+Text)
            }     
        }
        elseif (Command == "help") {
            Array = array(
            "!title (name) to change hotel title",
            "!desc (text) to change hotel description",
            "!price (amount)",
            "!addtime (VIP number) (amount in minutes)",
            "!evict (VIP number) to kick out a VIP",
            "!notice (text) send a message to all VIPs")
            for (I = 1, Array:count()){
                Owner:msg(""+Array[I,string])
            }
        }
    }  
}

